buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'

jarJar.enable()

group = 'com.lovetropics.minigames'
archivesBaseName = 'LTMinigames'

ext.buildnumber = 0
project.buildnumber = System.getenv('BUILD_NUMBER') ?: 'custom'

if (System.getenv('GHA_RELEASE_WORKFLOW')) {
    version = "${mod_version}"
} else if (System.getenv('GITHUB_RUN_NUMBER')) {
    project.buildnumber = System.getenv('GITHUB_RUN_NUMBER')
    version = "${mod_version}-${release_type}+${buildnumber}-gha"
} else {
    project.buildnumber = System.getenv('BUILD_NUMBER') ?: 'custom'
    version = "${mod_version}-${release_type}+${buildnumber}"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'official', version: minecraft_version
    
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            taskName 'ltminigames_client'
            
            workingDirectory project.file('run')
            arg "-mixin.config=ltminigames.mixins.json"
            jvmArg '-Dmixin.env.disableRefMap=true'

            property 'forge.logging.console.level', 'info'

            mods {
                ltminigames {
                    source sourceSets.main
                }
            }
        }

        server {
            taskName 'ltminigames_server'
        
            workingDirectory project.file('run')
            arg "-mixin.config=ltminigames.mixins.json"
            jvmArg '-Dmixin.env.disableRefMap=true'

            property 'forge.logging.console.level', 'info'

            mods {
                ltminigames {
                    source sourceSets.main
                }
            }
        }
        
        data {
            taskName 'ltminigames_data'
        
            workingDirectory project.file('run')
            arg "-mixin.config=ltminigames.mixins.json"
            jvmArg '-Dmixin.env.disableRefMap=true'

            environment 'target', 'fmluserdevdata'

            property 'forge.logging.console.level', 'info'

            mods {
                ltminigames {
                    source sourceSets.main
                }
            }
            
            args '--mod', 'ltminigames', '--all', '--output', '"' + rootProject.file('src/generated/resources/') + '"',
                 '--existing', '"' + sourceSets.main.resources.srcDirs[0] + '"' 
            
        }
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

repositories {
    maven { // Registrate
        url "https://maven.tterrag.com/"
    }
    mavenLocal()
    flatDir {
        dirs 'libs/'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    
    // Utilize tropicraft worldgen code
    implementation fg.deobf('net.tropicraft:Tropicraft:9.3.0-beta+custom')

    jarJar(implementation(fg.deobf("com.tterrag.registrate:Registrate:MC${minecraft_version}-${registrate_version}"))) {
        jarJar.ranged(it, "[MC1.19-1.1.5,MC1.20)")
    }

    jarJar(implementation(fg.deobf("com.lovetropics.lib:LTLib:${ltlib_version}"))) {
        jarJar.ranged(it, "[1.2,1.3)")
    }

    // Annotation Processors
    if (System.getProperty("idea.sync.active") != "true") {
        annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    }

    testImplementation platform("org.junit:junit-bom:5.7.0")
    testImplementation "org.junit.jupiter:junit-jupiter"
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

mixin {
    add sourceSets.main, "ltminigames.refmap.json"
}

tasks.jarJar.configure {
    classifier ''
    finalizedBy('reobfJarJar')
}

reobf {
    jarJar { }
}

jar {
    classifier = 'slim'
    manifest {
        attributes([
            "Specification-Title": "ltminigames",
            "Specification-Vendor": "lovetropics",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"lovetropics",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": "ltminigames.mixins.json"
        ])
    }
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.java
}

publishing {
    tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
            artifact sourceJar
        }
    }

    repositories {
        if (project.hasProperty('mavendir')) {
            maven { url mavendir }
        }
    }
}
