buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
        mavenLocal()
        maven {url='https://dist.creeper.host/Sponge/maven'}
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}
plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'

group = 'com.lovetropics.minigames'
archivesBaseName = 'LTMinigames'

ext.buildnumber = 0
project.buildnumber = System.getenv('BUILD_NUMBER') ?: 'custom'
version = "${mod_version}-${release_type}+${buildnumber}"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    mappings channel: 'snapshot', version: '20200515-1.15.1'
    
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            taskName 'ltminigames_client'
            
            workingDirectory project.file('run')
            arg "-mixin.config=ltminigames.mixins.json"

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                ltminigames {
                    source sourceSets.main
                }
            }
        }

        server {
            taskName 'ltminigames_server'
        
            workingDirectory project.file('run')
            arg "-mixin.config=ltminigames.mixins.json"

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                ltminigames {
                    source sourceSets.main
                }
            }
        }
        
        data {
            taskName 'ltminigames_data'
        
            workingDirectory project.file('run')
            arg "-mixin.config=ltminigames.mixins.json"

            environment 'target', 'fmluserdevdata'

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                ltminigames {
                    source sourceSets.main
                }
            }
            
            args '--mod', 'ltminigames', '--all', '--output', '"' + rootProject.file('src/generated/resources/') + '"',
                 '--existing', '"' + sourceSets.main.resources.srcDirs[0] + '"' 
            
        }
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

repositories {
    mavenLocal()
}

configurations {
    shade
    shadelib
    implementation.extendsFrom shadelib
}

repositories {
    maven { // Registrate
        url "http://maven.tterrag.com/"
    }
    mavenLocal()
    flatDir {
       dirs 'libs/'
   }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    
    // Utilize tropicraft worldgen code
    implementation fg.deobf('net.tropicraft:Tropicraft:8.1.0-alpha+22')
    
    // Shaded deps
    def registrate = "com.tterrag.registrate:Registrate:MC${minecraft_version}-${registrate_version}"
    implementation fg.deobf(registrate)
    shade registrate
    
    def ltlib = "com.lovetropics.lib:LTLib:${ltlib_version}"
    implementation fg.deobf(ltlib)
    shade ltlib

    shadelib group: 'org.asynchttpclient', name: 'async-http-client', version: '2.5.0'

    // Annotation Processors
    annotationProcessor 'org.spongepowered:mixin:0.8:processor'
}

mixin {
    add sourceSets.main, "ltminigames.refmap.json"
}

shadowJar {
    configurations = [project.configurations.shade, project.configurations.shadelib]
    relocate 'com.tterrag.registrate', 'com.lovetropics.minigames.repack.registrate'
    relocate 'com.lovetropics.lib', 'com.lovetropics.minigames.repack.ltlib'
    classifier = ''
}

reobf {
    shadowJar {}
}

build.dependsOn shadowJar
build.dependsOn reobfShadowJar

jar {
    classifier = 'slim'
    manifest {
        attributes([
            "Specification-Title": "ltminigames",
            "Specification-Vendor": "lovetropics",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"lovetropics",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.java
}

publishing {
    tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
            artifact shadowJar
            artifact sourceJar
        }
    }

    repositories {
        if (project.hasProperty('mavendir')) {
            maven { url mavendir }
        }
    }
}

if (System.getProperty("idea.sync.active") == "true") {
    afterEvaluate {
        tasks.withType(JavaCompile).all {
            it.options.annotationProcessorPath = files()
        }
    }
}
